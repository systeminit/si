config.define_string_list("to-run", args = True)
cfg = config.parse()

# Define groups of services
groups = {
    "platform": [
        "jaeger",
        "nats",
        "otelcol",
        "postgres",
        "rabbitmq",
    ],
    "backend": [
        "council",
        "pinga",
        "veritech",
        "sdf",
        "module-index",
    ],
    "frontend": [
        "web",
    ],
}
# Add "all" group as a sorted set of all services
_all = {}
for group_values in groups.values():
    for value in group_values:
        _all.update({ value: True })
groups.update({ "all": sorted(_all.keys()) })

# Parse the CLI args to enable group names and/or individual service names
enabled_resources = []
for arg in cfg.get("to-run", []):
    if arg in groups:
        enabled_resources += groups[arg]
    else:
        enabled_resources.append(arg)
config.set_enabled_resources(enabled_resources)

# Default trigger mode to manual so that (importantly) backend services don't rebuild/restart
# automatically. This is opt-in in the Tilt UI in the `Mode` column
trigger_mode = TRIGGER_MODE_MANUAL

def _buck2_dep_inputs(target):
    cmd = [
        "buck2",
        "uquery",
        "\"inputs(deps('{}'))\"".format(target),
    ]
    file_paths = str(local(" ".join(cmd))).splitlines()

    return file_paths

# From the Tilt docs:
#
# > By default, Tilt will not let you develop against a remote cluster.
#
# The implication appears to be that if Tilt finds a configured Kubernetes setup on your system
# **and** it's a remote cluster, despite the fact that we are not using any Kubernetes features or
# capabilities, it will still try to connect and fail. Instead, we're going to disable this check
# and continue.
#
# - https://docs.tilt.dev/choosing_clusters.html#remote
# - https://docs.tilt.dev/api.html#api.allow_k8s_contexts
allow_k8s_contexts(k8s_context())

# Use Docker Compose to provide the platform services
docker_compose("./docker-compose.platform.yml")
compose_services = ["jaeger", "nats", "otelcol", "postgres", "rabbitmq"]
for service in compose_services:
    dc_resource(service, labels = ["platform"])

# Locally build and run `module-index`
module_index_target = "//bin/module-index:module-index"
local_resource(
    "module-index",
    labels = ["backend"],
    cmd = "buck2 build {}".format(module_index_target),
    serve_cmd = "buck2 run {}".format(module_index_target),
    allow_parallel = True,
    auto_init = False,
    resource_deps = [
        "otelcol",
        "postgres"
    ],
    deps = _buck2_dep_inputs(module_index_target),
    trigger_mode = trigger_mode
)

# Locally build and run `council`
council_target = "//bin/council:council"
local_resource(
    "council",
    labels = ["backend"],
    cmd = "buck2 build {}".format(council_target),
    serve_cmd = "buck2 run {}".format(council_target),
    allow_parallel = True,
    resource_deps = [
        "nats",
        "otelcol",
    ],
    deps = _buck2_dep_inputs(council_target),
    trigger_mode = trigger_mode,
)

# Locally build and run `pinga`
pinga_target = "//bin/pinga:pinga"
local_resource(
    "pinga",
    labels = ["backend"],
    cmd = "buck2 build {}".format(pinga_target),
    serve_cmd = "buck2 run {}".format(pinga_target),
    allow_parallel = True,
    resource_deps = [
        "council",
        "nats",
        "otelcol",
        "veritech",
    ],
    deps = _buck2_dep_inputs(pinga_target),
    trigger_mode = trigger_mode,
)

# Locally build and run `veritech`
veritech_target = "//bin/veritech:veritech"
local_resource(
    "veritech",
    labels = ["backend"],
    cmd = "buck2 build {}".format(veritech_target),
    serve_cmd = "SI_LOG=debug buck2 run {}".format(veritech_target),
    allow_parallel = True,
    resource_deps = [
        "nats",
        "otelcol",
    ],
    deps = _buck2_dep_inputs(veritech_target),
    trigger_mode = trigger_mode,
)

# Locally build and run `sdf`
sdf_target = "//bin/sdf:sdf"
local_resource(
    "sdf",
    labels = ["backend"],
    cmd = "buck2 build {}".format(sdf_target),
    serve_cmd = "buck2 run {}".format(sdf_target),
    allow_parallel = True,
    resource_deps = [
        "nats",
        "otelcol",
        "pinga",
        "postgres",
        "veritech",
    ],
    deps = _buck2_dep_inputs(sdf_target),
    trigger_mode = trigger_mode,
    readiness_probe = probe(
        period_secs  = 5,
        http_get = http_get_action(
            port = 5156,
            path = "/api/"
        ),
    ),
    links = [
        "localhost:5156",
    ],
)

# Locally build and run `web` in dev mode
web_target = "//app/web:dev"
local_resource(
    "web",
    labels = ["frontend"],
    cmd = "buck2 build {}".format(web_target),
    serve_cmd = "buck2 run {}".format(web_target),
    allow_parallel = True,
    resource_deps = [
        "sdf",
    ],
    readiness_probe = probe(
        period_secs  = 5,
        http_get = http_get_action(
            port = 8080,
        ),
    ),
    links = [
        link("http://127.0.0.1:8080", "web"),
        link("https://auth.systeminit.com", "auth"),
    ],
)
